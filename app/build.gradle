apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'kotlin-kapt'
kapt {
    correctErrorTypes = true
    mapDiagnosticLocations = true
}
apply plugin: 'kotlin-android-extensions'
androidExtensions {
    experimental = true
}

android {

    compileSdkVersion BuildConfigs.compileSdk

    defaultConfig {
        applicationId "app.soulcramer.arn"
        minSdkVersion BuildConfigs.minSdk
        targetSdkVersion BuildConfigs.targetSdk
        versionCode 1
        versionName "0.0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs "en", "fr"
    }


    dataBinding {
        enabled = true
    }

    flavorDimensions "dev"
    productFlavors {
        prod {
            dimension "dev"
        }

        dev {
            dimension "dev"
            // Compile just these resources
            resConfigs "en", "fr", "xxhdpi"
            // Forbid crashlytics to create a unique crash id on each build
            ext.alwaysUpdateBuildId = false
            // Disable crashlytics (code gen, etcâ€¦)
            ext.enableCrashlytics = false
            // Disable png crushing (reducing size of png)
            aaptOptions.cruncherEnabled false
            // Disable split
            splits.abi.enable = false
            splits.density.enable = false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        // Exclude AndroidX version files
        exclude 'META-INF/*.version'
        // Exclude consumer proguard files
        exclude 'META-INF/proguard/*'
        // Exclude the Firebase/Fabric/other random properties files
        exclude '/*.properties'
        exclude 'fabric/*.properties'
        exclude 'META-INF/*.properties'
    }

    buildTypes {
        debug {
            shrinkResources true
            minifyEnabled true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
}

dependencies {
    // Kotlin JDK
    implementation Libraries.kotlinStandardLibrary
    implementation Libraries.kotlinCoroutines

    implementation project(Modules.core)
    implementation project(Modules.repository)
    implementation project(Modules.model)


    // AndroidX
    implementation LibrariesAndroidX.activity
    implementation LibrariesAndroidX.fragment
    implementation LibrariesAndroidX.navigationFragment
    implementation LibrariesAndroidX.navigationUi
    implementation LibrariesAndroidX.recyclerView
    implementation LibrariesAndroidX.materialComponent
    implementation LibrariesAndroidX.constraintLayout
    implementation LibrariesAndroidX.core
    implementation LibrariesAndroidX.lifecycle
    implementation LibrariesAndroidX.emoji

    implementation Libraries.threetenbp
    implementation Libraries.threetenabp

    // DI

    implementation Libraries.koinAndroidXScope
    implementation Libraries.koinViewModel

    implementation Libraries.fastAdapter
    implementation Libraries.fastAdapterCommons
    implementation Libraries.fastAdapterExtensions
    implementation Libraries.fastAdapterExpandable

    // Leak Canary
    debugImplementation Libraries.leackCanary

    implementation Libraries.glide
    implementation Libraries.glideAnnotation
    kapt Libraries.glideCompiler

    implementation Libraries.glideOkhttp
    implementation Libraries.timberKt
}
